/*
	**************************************************
		MeSH queries
	**************************************************
*/

/*
	Which triples have “Ofloxacin” as object?
*/
SPARQL

select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?s ?p "Ofloxacin" .

}
;


/*
	All you ever wanted to know about the resource “Ofloxacin” (mesh:D015242)
*/
SPARQL

select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     <http://nlm.nih.gov#MeSH:D015242> ?p ?c .

}
;


/*
	Making use of PREFIX avoids typing long URIs
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     mesh:D015242 ?p ?c .

}
;


/*
	The pharmacological actions of Ofloxacin (mesh:D015242)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     mesh:D015242 mesh:pharmacologicalAction ?c .

}
;


/*
	The pharmacological actions of Ofloxacin, and their triples
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     mesh:D015242 mesh:pharmacologicalAction ?pa .
     ?pa ?p ?o .
}
;


/*
	The pharmacological actions of Ofloxacin, only the labels
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select *

from <http://mor.nlm.nih.gov/mesh2014>

where {

     mesh:D015242 mesh:pharmacologicalAction ?pa .
     ?pa <http://www.w3.org/2000/01/rdf-schema#label> ?label .

}
;


/*
	What are the descriptors/SCRs that have the PA Anti-Bacterial Agents? (direct)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptor

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa <http://www.w3.org/2000/01/rdf-schema#label> ?label
	 FILTER (?label = "Anti-Bacterial Agents") .

}
;


/*
	What are the descriptors/SCRs that have the PA Anti-Bacterial Agents? (filter)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptor

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label ?label
     FILTER (?label = "Anti-Bacterial Agents") .

}
;


/*
	What are the descriptors/SCRs and their names that have the PA Anti-Bacterial Agents?
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptor ?descriptorname

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label "Anti-Bacterial Agents" .
     ?descriptor rdfs:label ?descriptorname .

}
ORDER BY ASC(?descriptorname)
;


/*
	Supplementary Concept Records and their links to descriptors (1)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptor ?descriptorname ?SCR

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label "Anti-Bacterial Agents" .
     ?descriptor rdfs:label ?descriptorname .
     ?SCR mesh:isMappedToDescriptor ?descriptor .
 
}
ORDER BY ASC(?descriptorname)
;


/*
	Supplementary Concept Records and their links to descriptors (2)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptor ?descriptorname ?SCR ?SCRname

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label "Anti-Bacterial Agents" .
     ?descriptor rdfs:label ?descriptorname .
     _:b mesh:isMappedToDescriptor ?descriptor .
     ?SCR mesh:mappedData _:b .
     ?SCR rdfs:label ?SCRname .
 
}
ORDER BY ASC(?descriptorname)
;


/*
	Count the number of SCRs that have the PA Anti-Bacterial Agents
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select count(distinct ?SCR)

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label "Anti-Bacterial Agents" .
     ?descriptor rdfs:label ?descriptorname .
     _:b mesh:isMappedToDescriptor ?descriptor .
     ?SCR mesh:mappedData _:b .
     ?SCR rdfs:label ?SCRname .
 
}
;


/*
	Count the number of SCRs per Descriptor
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
select ?descriptorname count(distinct ?SCR) as ?count

from <http://mor.nlm.nih.gov/mesh2014>

where {

     ?descriptor mesh:pharmacologicalAction ?pa .
     ?pa rdfs:label "Anti-Bacterial Agents" .
     ?descriptor rdfs:label ?descriptorname .
     _:b mesh:isMappedToDescriptor ?descriptor .
     ?SCR mesh:mappedData _:b .
     ?SCR rdfs:label ?SCRname .
 
}
ORDER BY DESC(?count)
;


/*
	Transitive Closure: All upper descriptors for “Levofloxacin”
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

select distinct *

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>

where {

     ?descriptor rdfs:label "Levofloxacin" .
     ?descriptor skos:broader ?upperDescriptor .
     ?upperDescriptor rdfs:label ?descriptorname .

}
;


/*
	Transitive Closure: All upper descriptors for “Levofloxacin”, with hierarchy
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
select distinct ?upperDescriptor ?descriptorname ?tn 

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>

where {

     ?descriptor rdfs:label "Levofloxacin" .
     ?descriptor skos:broader ?upperDescriptor .
     ?upperDescriptor rdfs:label ?descriptorname .
     ?upperDescriptor mesh:treeNumber ?tn .

}
ORDER BY ASC(?tn)
;


/*
	All Quinolones (including indirect descendants) using the TC (fast)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

select distinct ?descriptorname ?tn

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>

where {

     ?descriptor rdfs:label "Quinolones" .
     ?lowerDescriptor skos:broader ?descriptor .
     ?lowerDescriptor rdfs:label ?descriptorname .
     ?lowerDescriptor mesh:treeNumber ?tn .

}
ORDER BY ASC(?tn)
;


/*
	All Quinolones (including indirect descendants) not using the TC (slow)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

select distinct ?descriptorname ?tn

from <http://mor.nlm.nih.gov/mesh2014>

where {
     ?descriptor rdfs:label "Quinolones" .
     ?descriptor mesh:treeNumber ?quinolonesTn .
     
     ?lowerDescriptor rdfs:label ?descriptorname .
     ?lowerDescriptor mesh:treeNumber ?tn
     FILTER (contains(?tn, ?quinolonesTn)).
}
ORDER BY ASC(?tn)
;


/*
	**************************************************
		MeSH+MEDLINE queries
	**************************************************
*/


/*
	Find citations indexed with “Quinolones” (or any descendant)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX ml: <http://mor.nlm.nih.gov/ml/>

select distinct ?pmid ?descriptorname

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>
from <http://mor.nlm.nih.gov/medline_fda_201401> 

where {
     ?descriptor rdfs:label "Quinolones" .
     ?lowerDescriptor skos:broader ?descriptor .
     ?lowerDescriptor rdfs:label ?descriptorname .

     ?mh ml:Descriptor ?lowerDescriptor .
     ?mhl ml:MeshHeading ?mh .
     ?citation ml:MeshHeadingList ?mhl .
     ?citation ml:PMID ?pmid .
}
;


/*
	Count citations indexed with “Quinolones” (or any descendant)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX ml: <http://mor.nlm.nih.gov/ml/>

select distinct ?descriptorname count(distinct ?pmid) as ?count

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>
from <http://mor.nlm.nih.gov/medline_fda_201401> 

where {
     ?descriptor rdfs:label "Quinolones" .
     ?lowerDescriptor skos:broader ?descriptor .
     ?lowerDescriptor rdfs:label ?descriptorname .

     ?mh ml:Descriptor ?lowerDescriptor .
     ?mhl ml:MeshHeading ?mh .
     ?citation ml:MeshHeadingList ?mhl .
     ?citation ml:PMID ?pmid .
}
ORDER BY ?count
;


/*
	Find citations and their titles indexed with “Quinolones” (or any descendant)
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX ml: <http://mor.nlm.nih.gov/ml/>

select distinct ?descriptorname ?pmid ?title 

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>
from <http://mor.nlm.nih.gov/medline_fda_201401> 

where {

     #All descriptors under Quinolones
     ?descriptor rdfs:label "Quinolones" .
     ?lowerDescriptor skos:broader ?descriptor .
     ?lowerDescriptor rdfs:label ?descriptorname .

     #All MEDLINE citations indexed with any of these descriptors 
     ?mh ml:Descriptor ?lowerDescriptor .
     ?mhl ml:MeshHeading ?mh .
     ?citation ml:MeshHeadingList ?mhl .
     ?citation ml:PMID ?pmid .

     #get article titles 
     ?citation ml:Article ?article .
     ?article ml:ArticleTitle ?title .
}
ORDER by ?descriptorname
;


/*
	Find citations indexed with “Quinolones” that have the word “child” in their title
*/
SPARQL
PREFIX mesh: <http://nlm.nih.gov#MeSH:>
PREFIX ml: <http://mor.nlm.nih.gov/ml/>

select distinct ?descriptorname ?pmid ?title 

from <http://mor.nlm.nih.gov/mesh2014>
from <http://mor.nlm.nih.gov/mesh_tc_2014>
from <http://mor.nlm.nih.gov/medline_fda_201401> 

where {

     #All descriptors under Quinolones
     ?descriptor rdfs:label "Quinolones" .
     ?lowerDescriptor skos:broader ?descriptor .
     ?lowerDescriptor rdfs:label ?descriptorname .

     #All medline citations indexed with any of these descriptors 
     ?mh ml:Descriptor ?lowerDescriptor .
     ?mhl ml:MeshHeading ?mh .
     ?citation ml:MeshHeadingList ?mhl .
     ?citation ml:PMID ?pmid .

     #get article titles and look for "child"
     ?citation ml:Article ?article .
     ?article ml:ArticleTitle ?title FILTER (contains(?title, "child")).
}
ORDER by ?descriptorname
;


/*
	**************************************************
*/
