#important - see _config.yml query-types for the list of allowable query types.

- name: Retrieve list of predicates in MeSH RDF
  notes: This query gives a complete list of MeSH RDF predicates
  type: Getting Started
  string: |+
   PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   
   SELECT DISTINCT ?p
   FROM <http://id.nlm.nih.gov/mesh2014>
   WHERE {
   
     ?s ?p ?o
     
   }

- name: Retrieve all meshv classes in MeSH RDF
  notes: This query gives a unique list of all MeSH classes
  type: Getting Started
  string: |+
   PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   
   SELECT DISTINCT ?class
   FROM <http://id.nlm.nih.gov/mesh2014>
   WHERE {
   
      ?s rdf:type ?class
      
    }
    ORDER BY ?class
    
- name: Which triples have 'Ofloxacin' as the object?
  notes: A basic subject, predicate, object triple in MeSH RDF.
  type: Getting Started
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
     ?s ?p mesh:D015242 .
   }

- name: The Pharmacologic Actions of Oflaxacin and their labels.
  notes: Show the MeSH headings and labels of Pharmacologic Actions for Oflaxacin.
  type: Getting Started
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
     mesh:D015242 vocab:pharmacologicalAction ?pa .
     ?pa rdfs:label ?paLabel
   }
   
- name: Descriptors/SCRs that have the Pharmacologic Action 'Anti-Bacterial Agents'?
  notes: A filter query of MeSH predicates based on a certain type of Pharmacologic Action
  type: Predicate
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT ?d ?dLabel
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
    ?d vocab:pharmacologicalAction ?pa.
    ?d rdfs:label ?dLabel .
    ?pa rdfs:label "Anti-Bacterial Agents"
   }
   
   ORDER BY ASC (?dLabel)
   
- name: Any MeSH descriptor that has an allowable qualifier of adverse effects.
  notes: MeSH has rules that govern which qualifiers can be used with which MeSH Headings.
  type: Descriptors and Qualifiers
  string: |+
     PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
     PREFIX vocab:  <http://id.nlm.nih.gov/mesh/vocab#>
     PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>  
     
     SELECT distinct ?d ?dLabel  
     FROM  <http://id.nlm.nih.gov/mesh2014>
     
     WHERE {  
        ?d vocab:allowableQualifier ?q .  
        ?q rdfs:label "adverse effects" .  
        ?d rdfs:label ?dLabel  
     }
     
- name: Any MeSH descriptor that is an ancestor of D005138 'Eyebrow',
  notes: The MeSH heading for 'Eybrow' can be retrieved through various paths in the MeSH tree.  This query demonstrates how to 'explode up' the MeSH tree to find all ancestors.
  type: Transitive
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#> 
   PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#> 
   PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
   
   SELECT ?treeNode ?ancestorTreeNode ?ancestor ?alabel
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
      mesh:D005138 vocab:treeNumber ?treeNode .
      ?treeNode skos:broaderTransitive+ ?ancestorTreeNode .
      ?ancestor vocab:treeNumber ?ancestorTreeNode .
      ?ancestor rdfs:label ?alabel
   }
   
   ORDER BY ?treeNode ?ancestorTreeNode
   
- name: Any MeSH descriptor that is a descendant of D009369 'Neoplasm'
  notes: Find all descendants (not just immediate children) of 'Neoplasm'.  This query 'explodes down' the MeSH tree.
  type: Transitive
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#> 
    PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#> 
    PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
   
    SELECT DISTINCT ?desc ?desc_label
    FROM <http://id.nlm.nih.gov/mesh2014>
   
    WHERE {
      mesh:D009369 vocab:treeNumber ?tn .
      ?desctn skos:broaderTransitive+ ?tn .
      ?desc vocab:treeNumber ?desctn .
      ?desc rdfs:label ?desc_label .
    }
    
    ORDER BY ?desc_label

- name: Any MeSH term ('D' or 'M') that has 'infection' as part of its name.
  notes: Find MeSH descriptors or sub-concepts based on string.
  type: String
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    
    SELECT *
    FROM <http://id.nlm.nih.gov/mesh2014>
    
    WHERE {
    ?id rdfs:label ?label .
    FILTER regex(?label, "infection","i")
    FILTER regex(?id,"[MD]")
    }
    
    ORDER BY (?label)
  
- name: What are all the new MeSH Descriptor in the 'Diseases' tree for a given year (2014).
  notes: This query leverages the MeSH 'C' tree to find new diseases in MeSH.
  type: Aggregation
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX vocab: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   PREFIX xsd: <http://www.w3.org/2001/XMLSchema>
   
   SELECT DISTINCT ?id ?label
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
     ?id vocab:dateEstablished ?dateEstablished .
     ?id rdfs:label ?label .
     ?id vocab:treeNumber ?tn
     FILTER(?dateEstablished = xsd::date("2014-01-01"))
     FILTER(REGEX(?tn,"C"))
     }
     
     ORDER BY ?label
    
- name: All the MeSH concepts (prefCon and subCon) and semantic types associated with D000647 'Amnesia'
  notes: MeSH Descriptors are collections of MeSH concepts.  The name of the preferred concept (prefCon) always contributes the name of the Descriptor.
  type: Descriptors and Concepts
  string: |+
   PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
   PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   
   CONSTRUCT {
    
    mesh:D000647 meshv:preferredConcept ?prefcon .
    ?prefcon ?p ?o .
    ?prefcon meshv:semanticType $semtype .
    ?prefcon skos:narrower $subcon .
    $subcon rdfs:label $subconLabel .
    $semtype ?stp $sto .
    
    }
    
    FROM <http://id.nlm.nih.gov/mesh2014>
    
    WHERE {
    
    mesh:D000647 meshv:preferredConcept ?prefcon .
    ?prefcon ?p ?o .
    ?prefcon meshv:semanticType $semtype .
    ?prefcon skos:narrower $subcon .
    $subcon rdfs:label $subconLabel .
    $semtype ?stp $sto .
    
    }