#important - see _config.yml query-types for the list of allowable query types.

- name: Retrieve a list of predicates in MeSH RDF
  notes: This query gives a complete list of MeSH RDF predicates
  type: Getting Started
  string: |+
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   
   SELECT DISTINCT ?p
   FROM <http://id.nlm.nih.gov/mesh>
   WHERE {
   
     ?s ?p ?o
     
   }
   ORDER BY ?p

- name: Retrieve all meshv classes in MeSH RDF
  notes: This query gives a unique list of all MeSH classes
  type: Getting Started
  string: |+
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
   
   SELECT DISTINCT ?class
   FROM <http://id.nlm.nih.gov/mesh>
   WHERE {
   
      ?s rdf:type ?class
      
    }
    ORDER BY ?class
    
- name: Find triples with 'Ofloxacin' as the object
  notes: A basic subject, predicate, object triple in MeSH RDF.
  type: Getting Started
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh>
   
   WHERE {
     ?s ?p mesh:D015242 .
   }

- name:  Find the MeSH headings and labels of Pharmacological Actions for 'Ofloxacin'
  notes: Show the MeSH headings and labels of Pharmacological Actions for Ofloxacin.
  type: Getting Started
  string: |+
   
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh>
   
   WHERE {
     mesh:D015242 meshv:pharmacologicalAction ?pa .
     ?pa rdfs:label ?paLabel
   }
   
- name: Descriptors and SCRs that have the Pharmacological Action 'Anti-Bacterial Agents'
  notes: A filter query of MeSH predicates based on a certain type of Pharmacological Action
  type: Predicate
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT ?d ?dLabel
   FROM <http://id.nlm.nih.gov/mesh>
   
   WHERE {
    ?d meshv:pharmacologicalAction ?pa.
    ?d meshv:active 1 .
    ?d rdfs:label ?dLabel .
    ?pa rdfs:label "Anti-Bacterial Agents"@en
   }
   
   ORDER BY ASC (?dLabel)
   
- name: Find all active MeSH descriptors with an allowable qualifier of 'adverse effects'
  notes: MeSH has rules that govern which qualifiers can be used with which MeSH Headings.
  type: Descriptors and Qualifiers
  string: |+
     PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
     PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
     PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
     PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
     PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
     
     SELECT distinct ?d ?dLabel  
     FROM  <http://id.nlm.nih.gov/mesh>
     
     WHERE {  
        ?d meshv:allowableQualifier ?q .
        ?d meshv:active 1 .
        ?q rdfs:label "adverse effects"@en .  
        ?d rdfs:label ?dLabel  
     }
     
- name: Find all MeSH descriptors that are ancestors of MeSH descriptor D005138 'Eyebrow'
  notes: The MeSH heading for 'Eybrow' can be retrieved through various paths in the MeSH tree.  This query demonstrates how to 'explode up' the MeSH tree to find all ancestors.
  type: Transitive
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT ?treeNum ?ancestorTreeNum ?ancestor ?alabel
   FROM <http://id.nlm.nih.gov/mesh>
   
   WHERE {
      mesh:D005138 meshv:treeNumber ?treeNum .
      ?treeNum meshv:parentTreeNumber+ ?ancestorTreeNum .
      ?ancestor meshv:treeNumber ?ancestorTreeNum .
      ?ancestor rdfs:label ?alabel
   }
   
   ORDER BY ?treeNum ?ancestorTreeNum
   
- name: Find all MeSH descriptors that are descendants of MeSH descriptor D009369 'Neoplasm'
  notes: Find all descendants (not just immediate children) of 'Neoplasm'.  This query 'explodes down' the MeSH tree.
  type: Transitive
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
    PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
    SELECT DISTINCT ?descriptor ?label
    FROM <http://id.nlm.nih.gov/mesh>
   
    WHERE {
      mesh:D009369 meshv:treeNumber ?treeNum .
      ?childTreeNum meshv:parentTreeNumber+ ?treeNum .
      ?descriptor meshv:treeNumber ?childTreeNum .
      ?descriptor rdfs:label ?label .
    }
    
    ORDER BY ?label

- name: Retrieve all MeSH descriptors or concepts with 'infection' anywhere in its name
  notes: Find MeSH descriptors or sub-concepts based on string.
  type: String
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
   PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
    SELECT ?d ?dName ?c ?cName
    FROM <http://id.nlm.nih.gov/mesh>
    
    WHERE {
      
    ?d a meshv:Descriptor .
    ?d meshv:active 1 .
    ?d meshv:concept ?c .
    ?d rdfs:label ?dName .
    ?c rdfs:label ?cName
    FILTER(REGEX(?dName,"infection","i") || REGEX(?cName,"infection","i"))
    
    }
    ORDER BY ?d
  
- name: Find all MeSH Descriptors created for 2016 MeSH that are treed under diseases.
  notes: This query leverages the MeSH 'C' tree to find new diseases in MeSH.
  type: Aggregation
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
    PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
   
    SELECT DISTINCT ?d ?name
    FROM <http://id.nlm.nih.gov/mesh>
    
    WHERE {
    
    ?d a meshv:Descriptor .
    ?d rdfs:label ?name .
    ?d meshv:treeNumber ?tn .
    ?d meshv:dateEstablished ?dateEstablished
    FILTER(?dateEstablished = xsd::date("2016-01-01"))
    FILTER(REGEX(?tn,"C"))
   
    }
    
    ORDER BY ?name
    
- name: Retrieve all terms that belong to Descriptor D009369 'Neoplasm'.
  notes: The query uses inferencing to get all meshv:term labels and their altLabels (lexical variants)
  type: Lexical Variation
  string: |+
     PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
     PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
     PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
     PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
     PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
     PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
     
     SELECT ?t ?pred ?label
     
     FROM <http://id.nlm.nih.gov/mesh>
     
       WHERE { 
       mesh:D009369 meshv:concept ?c .
       ?c meshv:term ?t .
       ?t rdfs:label ?label .
       ?t ?pred ?label
       } 
    
- name: Display MeSH Qualifiers grouped by the their descriptors' base tree numbers.
  string: |+
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
    PREFIX owl: <http://www.w3.org/2002/07/owl#>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
    PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
    
    SELECT ?qualifier ?baseTreeNum (count(?baseTreeNum) AS ?count) WHERE {
    SELECT ?qualifier (SUBSTR(?tnLabel,1,3) AS ?baseTreeNum)
    
      WHERE {
      ?dq a meshv:AllowedDescriptorQualifierPair .
      ?dq meshv:hasDescriptor ?d .
      ?dq meshv:hasQualifier ?q .
      ?d meshv:treeNumber ?tn .
      ?tn rdfs:label ?tnLabel .
      ?q meshv:preferredTerm ?pt .
      ?pt meshv:prefLabel ?qualifier
      FILTER(?qualifier = 'adverse effects'@en)
        #full list of qualifiers to choose from:
        #http://www.nlm.nih.gov/mesh/topsubscope.html
       }
    }
    GROUP BY ?qualifier ?baseTreeNum
    ORDER BY ?baseTreeNum

- name: Retrieve a list of descriptors that are no longer active in current MeSH.
  string: |+
     PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
     PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
     PREFIX owl: <http://www.w3.org/2002/07/owl#>
     PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
     PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
     PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
     PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
     
          SELECT *
          FROM <http://id.nlm.nih.gov/mesh>
          WHERE { 
             ?d a meshv:Descriptor .
             ?d meshv:active 0 .
             ?d rdfs:label ?name
 
            }

